description = 'GAE selenium testing of test-app'

apply plugin: 'war'
apply plugin: 'appengine'

System.properties['appengine.sdk.root'] = System.properties['user.home'] + "/appengine-java-sdk-${gae_version}"

appengine {
    disableUpdateCheck = true
    
     endpoints {
            getClientLibsOnBuild = false
            getDiscoveryDocsOnBuild = false
        }
}

dependencies {
	compile libs.servlet_spec
	compile libs.guice
	compile libs.guice_servlet
	compile libs.inject
	compile project(path: ':blobstore-api', configuration: 'testCompile')
    compile project(path: ':blobstore-gae')
    compile project(path: ':test-app')
    
    functionalTestCompile project(path: ':blobstore-api', configuration: 'testCompile')
    functionalTestCompile project(path: ':blobstore-gae')
    functionalTestCompile libs.guiceberry
    functionalTestCompile libs.guava_testlib
    functionalTestCompile libs.junit
    functionalTestCompile libs.inject
    functionalTestCompile libs.guice
}

task unpackTest(dependsOn: ":blobstore-api:packageTests") << {
    def myTests = file("../blobstore-api/build/libs/blobstore-api-${version}-tests.jar")
    ant.unjar(dest: "$buildDir/classes/functionalTest", src: myTests)
}

task unpackTestApp(dependsOn: ":test-app:war") << {
    def app = file("../test-app/build/libs/test-app-${version}.war")
    ant.unjar(dest: "$buildDir/exploded-app/", src: app)
}
appengineRun.dependsOn unpackTest, unpackTestApp

appengineFunctionalTest {
	systemProperties['GuiceBerryEnvSelectorOverride_com.googlecode.simpleblobstore.PlaceHolderGuiceBerryEnv'] =
						'com.googlecode.simpleblobstore.gae.GaeGuiceBerryEnv'
    testLogging.showStandardStreams = true
}

clean.dependsOn appengineStop
